Imports System.Windows.Forms
Imports System.Configuration.ConfigurationManager
Imports Dac
Imports Dac.Tables

''' <summary>
''' 通番ロジッククラス
''' </summary>
''' <remarks></remarks>
Public Class TubanLogic

#Region "プロパティ"
    ''' <summary>
    ''' 個人リスト
    ''' </summary>
    Private Property Persons As List(Of Person) = New List(Of Person)


    ''' <summary>
    ''' DB接続情報
    ''' </summary>
    Public Property ConnectionInfo As List(Of String) = New List(Of String)
#End Region

#Region "通番リストを表示する"
    ''' <summary>
    ''' 通番リストを表示する
    ''' </summary>
    ''' <param name="flp"></param>
    ''' <param name="kenshinName"></param>
    ''' <param name="flg"></param>
    ''' <remarks></remarks>
    Public Sub DisplayList(flp As FlowLayoutPanel, kenshinName As String, flg As Boolean)
        Dim Dac As DBAccess = New DBAccess()
        Dac.ConnectInfo = ConnectionInfo

        Dim dbPerson As List(Of 被験者) = Dac.GetPersonList()

        ClearList()

        For Each psn In dbPerson
            Dim person As Person = New Person
            person.PersonID = psn.依頼者KEY
            person.lblTuban.Text = psn.日通番.ToString("000")
            person.lblName.Text = psn.被験者名

            Dim wkKenshin As String = String.Empty
            Select Case kenshinName
                Case CommonConst.SIRYOKU, CommonConst.SIRYOKU_SCHOOL
                    wkKenshin = psn.視力終了
                Case CommonConst.KETSUATSU
                    wkKenshin = psn.血圧終了
                Case CommonConst.SHINSATSU
                    wkKenshin = psn.診察終了
                Case CommonConst.CHYOURYOKU
                    wkKenshin = psn.聴力終了
                Case CommonConst.FUKUI
                    wkKenshin = psn.腹囲終了
                Case CommonConst.KENNYOU
                    wkKenshin = psn.尿検査終了
                Case Else

            End Select

            Select Case kenshinName
                Case CommonConst.SIRYOKU_SCHOOL
                    If Dac.IsInspectionCode(psn.依頼者KEY, AppSettings("NakedEyeR_School")) Then
                        Persons.Add(person)
                    End If
                Case Else
                    Persons.Add(person)
            End Select

            ' 受診済かどうかで色を変える
            If wkKenshin.Equals("N") Then
                person.BackColor = Drawing.Color.Beige
                person.lblTuban.BackColor = Drawing.Color.Beige
                person.lblName.BackColor = Drawing.Color.Beige
                person.Visible = True
            Else
                person.BackColor = Drawing.Color.LightPink
                person.lblTuban.BackColor = Drawing.Color.LightPink
                person.lblName.BackColor = Drawing.Color.LightPink
                If Not flg Then
                    person.Visible = False
                    Continue For
                End If
            End If

        Next

        For Each cntl In Persons
            flp.Controls.Add(cntl)
        Next

    End Sub

#End Region

#Region "リストを破棄する。"
    ''' <summary>
    ''' リストを破棄する。
    ''' </summary>
    ''' <remarks></remarks>
    Public Sub ClearList()
        For Each cntl In Persons
            cntl.Dispose()
        Next
    End Sub

#End Region
End Class

